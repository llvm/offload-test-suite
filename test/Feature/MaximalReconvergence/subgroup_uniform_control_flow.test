#--- source.hlsl
RWStructuredBuffer<float> Out : register(u0);

// This test checks that compilers do not perform optimizations, such as code hoisting,
// that may change the expected behavior when `-fspv-enable-maximal-reconvergence` flag
// is set.
// Here, all the blocks inside the branches are the same but they should not be taken out
// of the branch because it will change the result.

[numthreads(8,1,1)]
void main(uint3 TID : SV_GroupThreadID) {
    float maxActiveLaneCount = min(WaveActiveSum(1), 8);
    float div8 = 8.0/maxActiveLaneCount;

    float result = 0;

    // First non-uniform branch
    if (TID.x % 2 == 0) {
        // Second non-uniform branch
        if (TID.x % 4 == 0) {
            result += WaveActiveSum(div8);
        } else {
            result += WaveActiveSum(div8);
        }
        // Must reconverge here with maximal reconvergence
        result += WaveActiveSum(div8); 
    } else {
        result += WaveActiveSum(div8);
        result += WaveActiveSum(div8);
    }

    // Must reconverge here with maximal reconvergence
    result += WaveActiveSum(div8);
    Out[TID.x] = result;
}

//--- pipeline.yaml

---
Shaders:
  - Stage: Compute
    Entry: main
    DispatchSize: [1, 1, 1]
Buffers:
  - Name: Out
    Format: Float32
    Data: [ 0, 0, 0, 0, 0, 0, 0, 0 ]
DescriptorSets:
  - Resources:
    - Name: Out
      Kind: RWStructuredBuffer
      DirectXBinding:
        Register: 0
        Space: 0
      VulkanBinding:
        Binding: 0
...

#--- end
# UNSUPPORTED: Vulkan && !VK_KHR_shader_maximal_reconvergence

# BUG: https://github.com/llvm/llvm-project/issues/165288
# XFAIL: !WARP && Clang && (DirectX || Metal)

# RUN: split-file %s %t
# RUN: %dxc_target -T cs_6_0 -fspv-enable-maximal-reconvergence -Fo %t.o %t/source.hlsl
# RUN: %offloader %t/pipeline.yaml %t.o | FileCheck %s

# CHECK: Name: Out
# CHECK: Format: Float32
# CHECK: Data: [ 14, 16, 14, 16, 14, 16, 14, 16 ]
