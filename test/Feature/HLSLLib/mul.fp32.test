#--- source.hlsl

StructuredBuffer<float> In : register(t0);
RWStructuredBuffer<float> Out : register(u1);

[numthreads(1,1,1)]
void main() {
  float fs = In[0];   // 2.3
  float4 fv = float4(In[1], In[2], In[3], In[4]); // (1.1,2.75,3.3,4.5)
  float2x3 fm23 = float2x3(In[5], In[6], In[7],
                           In[8], In[9], In[10]); // ((1.7,2.5,3.125),(4.1,5.5,6.75))
  float3x2 fm32 = float3x2(In[11], In[12],
                           In[13], In[14],
                           In[15], In[16]); // ((1.25,2.9),(3.5,4.25),(5.75,6.3))

  // Overload 1: scalar * scalar
  Out[0] = mul(fs, 3.7);                    // 2.3*3.7 = 8.51

  // Overload 2: scalar * vector
  float4 r2 = mul(fs, fv);                  // 2.3*(1.1,2.75,3.3,4.5) = (2.53,6.325,7.59,10.35)
  Out[1] = r2.x;
  Out[2] = r2.y;
  Out[3] = r2.z;
  Out[4] = r2.w;

  // Overload 3: scalar * matrix
  float2x2 r3 = mul(fs, float2x2(1.25, 2.9, 3.5, 4.1));
  Out[5] = r3[0][0];                        // 2.875
  Out[6] = r3[0][1];                        // 6.67
  Out[7] = r3[1][0];                        // 8.05
  Out[8] = r3[1][1];                        // 9.43

  // Overload 4: vector * scalar
  float4 r4 = mul(fv, 3.7);                 // (1.1,2.75,3.3,4.5)*3.7 = (4.07,10.175,12.21,16.65)
  Out[9] = r4.x;
  Out[10] = r4.y;
  Out[11] = r4.z;
  Out[12] = r4.w;

  // Overload 5: vector * vector (dot product)
  Out[13] = mul(fv, fv);                    // 1.21+7.5625+10.89+20.25 = 39.9125

  // Overload 6: vector * matrix (1x2 * 2x3 = 1x3)
  float3 r6 = mul(float2(In[1], In[2]), fm23);
  Out[14] = r6.x;                           // 1.1*1.7+2.75*4.1 = 13.145
  Out[15] = r6.y;                           // 1.1*2.5+2.75*5.5 = 17.875
  Out[16] = r6.z;                           // 1.1*3.125+2.75*6.75 = 22.0

  // Overload 7: matrix * scalar
  float2x2 r7 = mul(float2x2(1.25, 2.9, 3.5, 4.1), fs);
  Out[17] = r7[0][0];                       // 2.875
  Out[18] = r7[0][1];                       // 6.67
  Out[19] = r7[1][0];                       // 8.05
  Out[20] = r7[1][1];                       // 9.43

  // Overload 8: matrix * vector (2x3 * 3x1 = 2x1)
  float2 r8 = mul(fm23, float3(In[1], In[2], In[3]));
  Out[21] = r8.x;                           // 1.7*1.1+2.5*2.75+3.125*3.3 = 19.0575
  Out[22] = r8.y;                           // 4.1*1.1+5.5*2.75+6.75*3.3 = 41.91

  // Overload 9: matrix * matrix (2x3 * 3x2 = 2x2)
  float2x2 r9 = mul(fm23, fm32);
  Out[23] = r9[0][0];                       // 1.7*1.25+2.5*3.5+3.125*5.75 = 28.84375
  Out[24] = r9[0][1];                       // 1.7*2.9+2.5*4.25+3.125*6.3 = 35.2425
  Out[25] = r9[1][0];                       // 4.1*1.25+5.5*3.5+6.75*5.75 = 63.1875
  Out[26] = r9[1][1];                       // 4.1*2.9+5.5*4.25+6.75*6.3 = 77.79
}

//--- pipeline.yaml

---
Shaders:
  - Stage: Compute
    Entry: main
    DispatchSize: [1, 1, 1]
Buffers:
  - Name: In
    Format: Float32
    Stride: 4
    Data: [ 2.3, 1.1, 2.75, 3.3, 4.5, 1.7, 2.5, 3.125, 4.1, 5.5, 6.75, 1.25, 2.9, 3.5, 4.25, 5.75, 6.3 ]
  - Name: Out
    Format: Float32
    Stride: 4
    FillSize: 108
  - Name: Expected
    Format: Float32
    Stride: 4
    Data: [ 8.51, 2.53, 6.325, 7.59, 10.35, 2.875, 6.67, 8.05, 9.43, 4.07, 10.175, 12.21, 16.65, 39.9125, 13.145, 17.875, 22.0, 2.875, 6.67, 8.05, 9.43, 19.0575, 41.91, 28.84375, 35.2425, 63.1875, 77.79 ]
Results:
  - Result: Result
    Rule: BufferFloatULP
    ULPT: 2
    Actual: Out
    Expected: Expected
DescriptorSets:
  - Resources:
    - Name: In
      Kind: StructuredBuffer
      DirectXBinding:
        Register: 0
        Space: 0
      VulkanBinding:
        Binding: 0
    - Name: Out
      Kind: RWStructuredBuffer
      DirectXBinding:
        Register: 1
        Space: 0
      VulkanBinding:
        Binding: 1
...
#--- end

# Not implemented https://github.com/llvm/llvm-project/issues/99138
# XFAIL: Clang

# RUN: split-file %s %t
# RUN: %dxc_target -T cs_6_0 -Fo %t.o %t/source.hlsl
# RUN: %offloader %t/pipeline.yaml %t.o
