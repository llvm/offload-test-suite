#--- source.hlsl
RWStructuredBuffer<uint4> Out : register(u0);

groupshared uint4 SharedData;
groupshared uint4 Indices[128];

// Note: Placing GroupMemoryBarrierWithGroupSync in divergent control branches
// is undefined, and hence, untested

[numthreads(128,4,1)]
void main(uint3 ThreadID : SV_GroupThreadID) {

  // Basic Broadcast
  if (ThreadID.x == 127 && ThreadID.y == 3) {
    SharedData = 1;
  }

  // Prevents SharedData being read below before being initialized
  GroupMemoryBarrierWithGroupSync();

  if (ThreadID.x == 0) {
    Out[0][ThreadID.y] = SharedData[ThreadID.y];
  }

  // Prevents SharedData being updated below before written to Out[0]
  GroupMemoryBarrierWithGroupSync();

  // Interlocked Accumulation
  for (uint I = 0; I < 128; I++) {
    if (ThreadID.x == I) {
      SharedData[ThreadID.y] = SharedData[ThreadID.y] + 1;
    }

    // Prevents SharedData datarace across ThreadID.x, and,
    // SharedData being written before fully accumulated
    GroupMemoryBarrierWithGroupSync();
  }

  if (ThreadID.x == 127) {
    Out[1][ThreadID.y] = SharedData[ThreadID.y];
  }

  // Strided Read/Write:
  Indices[ThreadID.x][ThreadID.y] = ThreadID.x + ThreadID.y * 128;

  // Prevents Indices from being read before initialized
  GroupMemoryBarrierWithGroupSync();

  uint2 MappedIdx = {(ThreadID.x * 37) % 128, (ThreadID.y * 3 % 4)};
  uint ToIdx = Indices[MappedIdx.x][MappedIdx.y];

  if (ThreadID.x == 23) { // 23 is picked arbitrarily
    // Expected mapping:
    //   x: 23 -> 83, y: 0 -> 0, 1 -> 3, 2 -> 2
    //
    // Out[2] = [83, 83 + 3 * 128, 83 + 2 * 128, 83 + 128]
    Out[2][ThreadID.y] = ToIdx;
  }
}

//--- pipeline.yaml

---
Shaders:
  - Stage: Compute
    Entry: main
    DispatchSize: [1, 1, 1]
Buffers:
  - Name: Out
    Format: UInt32
    Channels: 4
    ZeroInitSize: 48
  - Name: ExpectedOut
    Format: UInt32
    Channels: 4
    Data: [
      1, 1, 1, 1,         # Broadcast
      129, 129, 129, 129, # Accumulation
      83, 467, 339, 211,  # Strided Read/Write
    ]
Results:
  - Result: ExpectedOut
    Rule: BufferExact
    Actual: Out
    Expected: ExpectedOut
DescriptorSets:
  - Resources:
    - Name: Out
      Kind: RWStructuredBuffer
      DirectXBinding:
        Register: 0
        Space: 0
      VulkanBinding:
        Binding: 0
...
#--- end

# No lowering to DXIL Barrier/SPIRV OpMemoryBarrier
# Unsupported https://github.com/llvm/llvm-project/issues/99121
# UNSUPPORTED: Clang

# RUN: split-file %s %t
# RUN: %dxc_target -T cs_6_5 -Fo %t.o %t/source.hlsl
# RUN: %offloader %t/pipeline.yaml %t.o
